|00 @System &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|90 @Mouse &vector $2 &x $2 &y $2 &state $1 &chord $1 &pad $4 &scrolly &scrolly-hb $1 &scrolly-lb $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|0100

@on-reset ( -> )
	( | theme )
	#f00f .System/r DEO2
	#f006 .System/g DEO2
	#f002 .System/b DEO2
	( | size )
	#0200 .Screen/width DEO2
	#00c0 .Screen/height DEO2
	<redraw>
	;on-mouse .Mouse/vector DEO2
	;on-console .Console/vector DEO2
	BRK

@on-console ( -> )
	( | append )
	.Console/read DEI ;buf scap/ STA
	<redraw>
	BRK

@on-mouse ( -> )
	[ LIT2 00 -Mouse/state ] DEI NEQ #41 ADD ;cursor-icn <update-cursor>
	BRK

@<redraw> ( -- )
	#0014 .Screen/x DEO2
	#0004 .Screen/y DEO2
	;buf <draw-text>
	<draw-scrollbar>
	JMP2r

@<draw-text> ( str* -- )
	#15 .Screen/auto DEO
	&w ( -- )
		handle-wrap LDAk #20 SUB #00 SWP #40 SFT2 ;font-icn ADD2 .Screen/addr DEO2
		#0a .Screen/sprite DEO
		&res INC2 LDAk ?&w
	POP2 JMP2r

@handle-wrap ( str* -- str* )
	LDAk #20 GTH ?{
		INC2k word-fits ?{ <draw-lb>
			!<draw-text>/res }
		}
	LDAk #0a NEQ ?{ <draw-lb>
		!<draw-text>/res }
	JMP2r

@word-fits ( str* -- f )
	.Screen/x DEI2 STH2
	&w ( -- )
		STH2kr .Screen/width DEI2 GTH2 ?&fail
		LDAk #20 GTH ?{
			POP2 POP2r #01 JMP2r }
		[ LIT2r 0008 ADD2r ] INC2 !&w
	JMP2r
	&fail POP2 POP2r #00 JMP2r

@<draw-lb> ( -- )
	.Screen/y DEI2k #0010 ADD2 ROT DEO2
	#0014 .Screen/x DEO2
	JMP2r

@<draw-scrollbar> ( -- )
	#0000 DUP2 .Screen/x DEO2
	.Screen/y DEO2
	( | fill )
	#12 .Screen/auto DEO
	;halftone-icn .Screen/addr DEO2
	#00 .Screen/height DEI2 #03 SFT2 NIP SUB
	&times ( -- )
		[ LIT2 01 -Screen/sprite ] DEO
		INC DUP ?&times
	POP
	( | icon )
	#0000 .Screen/y DEO2
	;resize-icn .Screen/addr DEO2
	#16 .Screen/auto DEO
	[ LIT2 01 -Screen/sprite ] DEOk DEO
	JMP2r

@<update-cursor> ( color addr* -- )
	[ LIT2 00 -Screen/auto ] DEO
	;fill-icn .Screen/addr DEO2
	#40 <draw-cursor>
	.Mouse/x DEI2 ,<draw-cursor>/x STR2
	.Mouse/y DEI2 ,<draw-cursor>/y STR2
	.Screen/addr DEO2

@<draw-cursor> ( color -- )
	[ LIT2 &x $2 ] .Screen/x DEO2
	[ LIT2 &y $2 ] .Screen/y DEO2
	.Screen/sprite DEO
	JMP2r

(
@|stdlib )

@scap ( str* -- end* )
	&w ( -- )
		INC2 & LDAk ?&w
	JMP2r

@<pword> ( str* -- )
	&w ( -- )
		LDAk #18 DEO
		INC2 LDAk #20 GTH ?&w
	POP2 JMP2r

@<phex> ( short* -- )
	SWP <phex>/b
	&b ( -- )
		DUP #04 SFT <phex>/c
	&c ( -- )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
		JMP2r

(
@|assets )

@cursor-icn [ 80c0 e0f0 f8e0 1000 ]

@fill-icn [ ffff ffff ffff ffff ]

@halftone-icn [ aa55 aa55 aa55 aa55 ]

@resize-icn [
	aa55 805f 9051 9357 aa55 02f9 0a89 cae9
	9753 9150 9f40 aa55 eac9 8a09 fa01 aa55 ]
~src/utils/log.assets.tal

