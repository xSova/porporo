|00 @System &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller &vector $2 &button $1 &key $1
|90 @Mouse &vector $2 &x $2 &y $2 &state $1 &chord $1 &pad $4 &scrolly &scrolly-hb $1 &scrolly-lb $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|0100

@on-reset ( -> )
	#f07f .System/r DEO2
	#f0d6 .System/g DEO2
	#f0b2 .System/b DEO2
	<load-theme>
	#0080 .Screen/width DEO2
	;dict/home-path <load-dir>
	;mem/dir count-lines #40 SFT2 #0010 ADD2 .Screen/height DEO2
	<redraw>
	;on-mouse .Mouse/vector DEO2
	;on-control .Controller/vector DEO2
	;mem/folders <perr>
	;mem/roms <perr>
	#010e DEO
	BRK

@<print-entries> ( addr* -- )
	&w ( -- )
		LDAk ?{ POP2 JMP2r }
		DUP2 <perr>
		#0a19 DEO
		scap/ INC2 !&w

@on-mouse ( -> )
	[ LIT2 00 -Mouse/state ] DEI NEQ #41 ADD ;cursor-icn <update-cursor>
	.Mouse/y DEI2 #0008 SUB2 #04 SFT2 NIP .Mouse/state DEI ?{ <hover>
		BRK }
	<select>
	#00 .Mouse/state DEO
	BRK

@on-control ( -> )
	.Controller/key DEI
	( esc ) DUP #1b NEQ ?{ <exit> }
	POP BRK

(
@|core )

@<exit> ( -- )
	#ff ;<hover>/last STA
	#800f DEO
	JMP2r

@<load-dir> ( path* -- )
	.File/name DEO2
	#1000 .File/length DEO2
	;mem/dir .File/read DEO2
	( | parse )
	;mem/dir
	&w ( -- )
		DUP2 <parse-line>
		lcap/ INC2 LDAk ?&w
	POP2 JMP2r

@<parse-line> ( line* -- )
	#0005 ADD2 LDAk [ LIT ". ] EQU ?&hidden
	( ) is-folder ?&folder
	( ) is-rom ?&rom
	;mem/misc scap/ !<append-line>
	&folder ( line* -- )
		;mem/folders scap/ !<append-line>
	&rom ( line* -- )
		;mem/roms scap/ !<append-line>
	&hidden ( line* -- )
		POP2 JMP2r

@<append-line> ( src* dst* -- )
	STH2
	&w ( -- )
		LDAk #1f GTH ?{
			LDA #00 STH2r STA2
			JMP2r }
		LDAk #00 STH2kr STA2
		INC2r INC2 !&w

@is-folder ( line* -- line* f )
	DUP2 lcap/ #0001 SUB2 LDA [ LIT "/ ] EQU JMP2r

@is-rom ( line* -- line*f )
	DUP2 lcap/ #0003 SUB2 ;dict/rom-ext !scmp3

@count-lines ( str* -- lines* )
	[ LIT2r 0000 ]
	&w ( -- )
		LDAk #0a NEQ ?{ INC2r }
		INC2 LDAk ?&w
	POP2 STH2r JMP2r

@<select> ( id -- )
	.Mouse/state DEI #01 NEQ ?<exit>
	DUP ?{
		;mem/dir #0005 ADD2 <pword>/ }
	STH
	[ LITr 00 ] ;mem/dir #0005 ADD2
	&w ( -- )
		LDAk #0a NEQ ?{
			#0006 ADD2 INCr EQUkr STHr ?&found }
		INC2 LDAk ?&w
	POP2r !<exit>
	&found ( str* | a b -- )
		POP2r <pword>/
		#0a18 DEO
		JMP2r

@<hover> ( id -- )
	[ LIT &last ff ] NEQk ?{ POP2 JMP2r }
	POP ,&last STR
	( >> )

@<redraw> ( -- )
	( | clear )
	#0000 DUP2 .Screen/x DEO2
	.Screen/y DEO2
	#80 .Screen/pixel DEO
	( | highlight )
	;fill-icn .Screen/addr DEO2
	#0000 .Screen/x DEO2
	#00 ;<hover>/last LDA #40 SFT2 #0008 ADD2 .Screen/y DEO2
	#f2 .Screen/auto DEO
	#02 .Screen/sprite DEOk DEO
	( | text )
	#0008 .Screen/x DEO2
	#0008 .Screen/y DEO2
	.Screen/x DEI2 ,&anchor STR2
	#15 .Screen/auto DEO
	;mem/dir #0005 ADD2
	&w ( -- )
		LDAk #0a NEQ ?{
			.Screen/y DEI2k #0010 ADD2 ROT DEO2
			[ LIT2 &anchor $2 ] .Screen/x DEO2
			#0006 ADD2 !&resume }
		LDAk #20 SUB #00 SWP
		( ) DUP2 #50 SFT2 ;font-uf2/glyphs ADD2 .Screen/addr DEO2
		( ) ;font-uf2 ADD2 LDA #00 SWP .Screen/x DEI2 ADD2
		( ) #05 .Screen/sprite DEOk DEO
		.Screen/x DEO2
		INC2 &resume LDAk ?&w
	POP2 JMP2r

@<update-cursor> ( color addr* -- )
	[ LIT2 00 -Screen/auto ] DEO
	;fill-icn .Screen/addr DEO2
	#40 <draw-cursor>
	.Mouse/x DEI2 ,<draw-cursor>/x STR2
	.Mouse/y DEI2 ,<draw-cursor>/y STR2
	.Screen/addr DEO2

@<draw-cursor> ( color -- )
	[ LIT2 &x $2 ] .Screen/x DEO2
	[ LIT2 &y $2 ] .Screen/y DEO2
	.Screen/sprite DEO
	JMP2r

(
@|stdlib )

@<scpy> ( src* dst* -- )
	STH2
	&w ( -- )
		LDAk #00 STH2kr STA2
		INC2r INC2 LDAk ?&w
	POP2 POP2r JMP2r

@slen ( str* -- len* )
	DUP2 scap/ SWP2 SUB2 JMP2r

@scap ( str* -- end* )
	&w ( -- )
		INC2 & LDAk ?&w
	JMP2r

@scmp3 ( a* b* -- f )
	STH2
	LDAkr LDAk STHr NEQ ?{ INC2r INC2 }
	LDA2r LDA2 STH2r EQU2 JMP2r

@lcap ( str* -- next-line* )
	&w ( -- )
		LDAk #0a EQU ?{
			INC2 & LDAk ?&w }
	JMP2r

@<perr> ( str* -- )
	&w ( -- )
		LDAk #19 DEO
		INC2 & LDAk ?&w
	POP2 JMP2r

@<pword> ( str* -- )
	&w ( -- )
		LDAk #18 DEO
		INC2 & LDAk #20 GTH ?&w
	POP2 JMP2r

(
@|theme )

@<load-theme> ( -- )
	;&path .File/name DEO2
	#0002 .File/length DEO2
	[ LIT2 -System/debug -System/r ]
	&l ( -- )
		;&buf .File/read DEO2
		.File/success DEI2 #0000 EQU2 ?{
			STHk [ LIT2 &buf $2 ] STHr DEO2
			INC INC NEQk ?&l }
	POP2 JMP2r
	&path ".theme $1

(
@|assets )

@cursor-icn [ 80c0 e0f0 f8e0 1000 ]

@fill-icn [ ffff ffff ffff ffff ]

@dict &home-path ". $1
	&rom-ext "rom $1

@mem &dir $1000
	&folders $800
	&roms $800
	&misc $800

~src/utils/font_losangeles.tal

