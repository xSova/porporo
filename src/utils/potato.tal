|00 @System &vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console &vector $2 &read $1 &pad $4 &type $1 &write $1 &error $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller &vector $2 &button $1 &key $1
|90 @Mouse &vector $2 &x $2 &y $2 &state $1 &chord $1 &pad $4 &scrolly &scrolly-hb $1 &scrolly-lb $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|c0 @DateTime &year $2 &month $1 &day $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1

|0000

	@action &vector $2 &value $1

|0100

@on-reset ( -> )
	;meta #06 DEO2
	#f17f .System/r DEO2
	#f1db .System/g DEO2
	#f1c4 .System/b DEO2
	<load-theme>
	.Screen/width DEI2 #0020 SUB2 .Screen/width DEO2
	#0018 .Screen/height DEO2
	;on-mouse .Mouse/vector DEO2
	;on-control .Controller/vector DEO2
	;on-screen .Screen/vector DEO2
	<redraw>
	( | register routines )
	;on-action .action/vector STZ2
	( #010e DEO ) BRK

@meta $1
	( name ) "Potato(Porporo) 0a
	( desc ) "Menu 20 "Bar 0a
	( auth ) "By 20 "Devine 20 "Lu 20 "Linvega 0a
	( date ) "13 20 "Dec 20 "2023 $1
	( exts ) 00

@on-screen ( -> )
	[ LIT &last $1 ] .DateTime/minute DEI DUP ,&last STR
	EQU ?{ <draw-time> }
	BRK

@on-mouse ( -> )
	[ LIT2 00 -Mouse/state ] DEI NEQ #41 ADD ;cursor-icn <update-cursor>
	.Mouse/state DEI ?&on-touch
	BRK
	&on-touch ( -> )
		#fe .Console/type DEO
		.Mouse/x DEI2 #04 SFT2 NIP .Console/write DEO
		#00 .Console/type DEO
		#00 .Mouse/state DEO
		#40 ;fill-icn <update-cursor>
		BRK

@on-control ( -> )
	BRK

(
@|remote )

@on-action ( -> )
	<redraw>
	BRK

(
@|core )

(
@|drawing )

@<redraw> ( -- )
	#0000 .Screen/x DEO2
	#0000 .Screen/y DEO2
	#80 .Screen/pixel DEO
	( | draw buttons )
	#00 .action/value LDZ #40 SFT2 .Screen/x DEO2
	<draw-button>
	#0040 .Screen/x DEO2
	#0006 .Screen/y DEO2
	[ LIT2 00 -action/value ] LDZ DUP ADD ;actions-lut ADD2 LDA2 <draw-uf2>
	<draw-date>
	<draw-time>
	JMP2r

@<draw-button> ( color -- )
	;fill-icn .Screen/addr DEO2
	#21 .Screen/auto DEO
	.action/value LDZ INC .Screen/sprite DEOk DEO
	JMP2r

@<draw-date> ( -- )
	#01 ;<draw-char>/color STA
	.Screen/width DEI2 #01 SFT2 .Screen/x DEO2
	[ #00 .DateTime/dotw DEI #20 SFT ] ;week-txt ADD2 <draw-uf2>
	[ LIT ", ] <draw-char>
	<draw-ws>
	.DateTime/day DEI <draw-dec>
	<draw-ws>
	[ #00 .DateTime/month DEI #20 SFT ] ;month-txt ADD2 <draw-uf2>
	<draw-ws>
	.DateTime/year DEI2 <draw-dec>
	JMP2r

@<draw-time> ( -- )
	#01 ;<draw-char>/color STA
	.Screen/width DEI2 #0030 SUB2 .Screen/x DEO2
	( | hour )
	.DateTime/hour DEI
	( ) DUP #0a DIV <draw-num>
	( ) #0a DIVk MUL SUB <draw-num>
	[ LIT ": ] <draw-char>
	( | minute )
	.DateTime/minute DEI
	( ) DUP #0a DIV <draw-num>
	( ) #0a DIVk MUL SUB <draw-num>
	JMP2r

@<draw-ws> ( -- )
	.Screen/x DEI2 #00 ;font-uf2 LDA ADD2 .Screen/x DEO2
	JMP2r

@<draw-dec> ( short* -- )
	#2710 [ LIT2r 00fb ]
	&w ( -- )
		DIV2k #000a DIV2k MUL2 SUB2 SWPr EQUk OVR STHkr EQU AND ?{ DUP <draw-num>
			INCr }
		POP2 #000a DIV2 SWPr INCr STHkr ?&w
	POP2r POP2 POP2 JMP2r

@<draw-uf2> ( addr* -- )
	#15 .Screen/auto DEO
	&w ( -- )
		LDAk #1f GTH ?{ POP2 JMP2r }
		LDAk <draw-char>
		INC2 !&w

@<draw-num> ( byte -- )
	[ LIT "0 ] ADD
	( >> )

@<draw-char> ( char -- )
	#20 SUB #00 SWP
	( ) DUP2 #50 SFT2 ;font-uf2/glyphs ADD2 .Screen/addr DEO2
	( ) ;font-uf2 ADD2 LDA #00 SWP .Screen/x DEI2 ADD2
	( ) [ LIT &color 05 ] .Screen/sprite DEOk DEO
	.Screen/x DEO2
	JMP2r

@<update-cursor> ( color addr* -- )
	[ LIT2 00 -Screen/auto ] DEO
	;fill-icn .Screen/addr DEO2
	#40 <draw-cursor>
	.Mouse/x DEI2 ,<draw-cursor>/x STR2
	.Mouse/y DEI2 ,<draw-cursor>/y STR2
	.Screen/addr DEO2

@<draw-cursor> ( color -- )
	[ LIT2 &x $2 ] .Screen/x DEO2
	[ LIT2 &y $2 ] .Screen/y DEO2
	.Screen/sprite DEO
	JMP2r

(
@|stdlib )

@<pstr> ( str* -- )
	&w ( -- )
		LDAk #18 DEO
		INC2 & LDAk ?&w
	POP2 JMP2r

@<phex> ( short* -- )
	SWP <phex>/b
	&b ( -- )
		DUP #04 SFT <phex>/c
	&c ( -- )
		#0f AND DUP #09 GTH #27 MUL ADD [ LIT "0 ] ADD #18 DEO
		JMP2r

(
@|theme )

@<load-theme> ( -- )
	;&path .File/name DEO2
	#0002 .File/length DEO2
	[ LIT2 -System/debug -System/r ]
	&l ( -- )
		;&buf .File/read DEO2
		.File/success DEI2 #0000 EQU2 ?{
			STHk [ LIT2 &buf $2 ] STHr DEO2
			INC INC NEQk ?&l }
	POP2 JMP2r
	&path ".theme $1

(
@|assets )

@cursor-icn [ 80c0 e0f0 f8e0 1000 ]

@fill-icn [ ffff ffff ffff ffff ]

@week-txt
	"Sun $1
	"Mon $1
	"Tue $1
	"Wed $1
	"Thu $1
	"Fri $1
	"Sat $1

@month-txt
	"Jan $1
	"Feb $1
	"Mar $1
	"Apr $1
	"May $1
	"Jun $1
	"Jul $1
	"Aug $1
	"Sep $1
	"Oct $1
	"Nov $1
	"Dec $1

@dict &default "Default $1
	&move "Move $1
	&draw "Draw $1

@actions-lut [ =dict/default =dict/move =dict/draw ]

~src/utils/potato.assets.tal

